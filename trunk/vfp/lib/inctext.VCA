SCCTEXT Version 4.0.0.2
PLATFORM C(8,0),UNIQUEID C(10,0),TIMESTAMP N(10,0),CLASS M(4,0),CLASSLOC M(4,0),BASECLASS M(4,0),OBJNAME M(4,0),PARENT M(4,0),PROPERTIES M(4,0),PROTECTED M(4,0),METHODS M(4,0),OBJCODE M(4,0),OLE M(4,0),OLE2 M(4,0),RESERVED1 M(4,0),RESERVED2 M(4,0),RESERVED3 M(4,0),RESERVED4 M(4,0),RESERVED5 M(4,0),RESERVED6 M(4,0),RESERVED7 M(4,0),RESERVED8 M(4,0),USER M(4,0)
1251

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] Class     
[START RESERVED1]
VERSION =   3.00[END RESERVED1]

[ RECORD]
[PLATFORM] WINDOWS 
[UNIQUEID] _0DZ15IIIU
[CLASS] textbox
[BASECLASS] textbox
[OBJNAME] inctext
[START PROPERTIES]
BorderStyle = 0
FontName = "MS Sans Serif"
Margin = 0
Name = "inctext"
ReadOnly = .T.
comparisonexpression = 
searchsrt = 
typefield = 
[END PROPERTIES]
[START PROTECTED]
comparisonexpression^
initsearch^
isactivesearch^
isseekmode^
search^
searchsrt^
searchwindow^
typefield^
[END PROTECTED]
[START METHODS]
PROCEDURE GotFocus
This.isSeekMode=.F.

ENDPROC
PROCEDURE KeyPress
LPARAMETERS nKeyCode, nShiftAltCtrl
	DO CASE
	CASE m.nKeyCode=10 AND THIS.isActiveSearch && Ctrl+Enter - продолжение поиска
	    NODEFAULT
	    THIS.Search(2)
	CASE BETWEEN(m.nKeyCode, 32, 126) OR ;
	    BETWEEN(m.nKeyCode, 192, 255) OR ;
	    m.nKeyCode = 128	&& AlphaNumeric
	    NODEFAULT
		* Определяем тип данных в текущей колонке
	    THIS.TypeField=TYPE(THIS.PARENT.CONTROLSOURCE)
		* Если тип поля по которому может проводиться поиск не верный, возврат
	    IF !(THIS.TypeField$"NDC")
	        RETURN
	    ENDIF
		* Если происходит активизация нового поиска, запускаем прогрраму его организации
	    IF !THIS.isActiveSearch
			* Обнуляем свойства учавствующие в поиске
	        THIS.ComparisonExpression=''
	        THIS.SearchSrt=UPPER(CHR(m.nKeyCode))
	        THIS.InitSearch
			* Выставляем флаг активизации в .T.
	        THIS.isActiveSearch=.T.
	    ELSE
	        THIS.SearchSrt=THIS.SearchSrt+UPPER(CHR(m.nKeyCode))
	    ENDIF
		*
	    THIS.SearchWindow.loSearchText.CAPTION=THIS.SearchSrt
	    THIS.Search(1)
	CASE m.nKeyCode=127 AND THIS.isActiveSearch && Backspace
	    NODEFAULT
	    THIS.SearchSrt = LEFT(THIS.SearchSrt,LEN(THIS.SearchSrt)-1)
	    THIS.SearchWindow.loSearchText.CAPTION=THIS.SearchSrt
	    THIS.Search(1)
	OTHERWISE
	    THIS.LOSTFOCUS
	ENDCASE
	* Выставляем флаг, что поиск уже проведен
	THIS.isSeekMode=.F.

ENDPROC
PROCEDURE LostFocus
IF ! THIS.isSeekMode
	* Деактивизация поиска
    IF THIS.isActiveSearch
		* Удаляем "окно" вывода символов поиска
        IF VARTYPE(THIS.SearchWindow)='O'
            THISFORM.REMOVEOBJECT(THIS.SearchWindow.NAME)
			* Перерисовываем грид
            THISFORM.REFRESH
            THIS.isActiveSearch=.F.
        ENDIF
    ENDIF
ENDIF
ENDPROC
PROCEDURE When
THIS.PARENT.PARENT.TAG=STR(RECNO())
ENDPROC
PROCEDURE initsearch
********************************************************************************
******************* Процедура подготовки к новому поиску ***********************
********************************************************************************
LOCAL lcSearchWindow as String, loI as Object, lcFieldName as String, lnFieldLen as Integer
	* Создам на форме 'окно' для показа в нем набераемых символов
	lcSearchWindow=SYS(2015)
	THISFORM.ADDOBJECT(m.lcSearchWindow,"CONTAINER")
	THIS.SearchWindow=THISFORM.&lcSearchWindow
	WITH THIS.SearchWindow
	    .WIDTH=THIS.PARENT.WIDTH+2
	    .HEIGHT=26
		*!!! Устанавливаем верх 'окошка' под последней целиком видимой строкой в гриде !!!
	    WITH THIS.PARENT.PARENT
			* Вычисляем общую высоту целиком видимых строк
	        THIS.SearchWindow.TOP=FLOOR(((.HEIGHT-.HEADERHEIGHT)-IIF(INLI(.SCROLLBARS,1,3),SYSMETRIC(8),0))/.ROWHEIGHT)*.ROWHEIGHT
	    ENDWITH
	    .TOP=.TOP+OBJTOCLIENT(THIS.PARENT.PARENT,1) && Прибовляем расстояние от верха формы до грида
	    .TOP=.TOP+THIS.PARENT.PARENT.HEADERHEIGHT && Прибавляем высоту заголовка грида
		*!!! Устанавливаем 'окошко' по горизонтали над текущим столбцом !!!
	    .LEFT=OBJTOCLIENT(THIS.PARENT.PARENT,2) && Расстояние от левого края формы до грида
	    .LEFT=.LEFT+IIF(THIS.PARENT.PARENT.DELETEMARK,8,0) && учитываем ширину DELETEMARK
	    .LEFT=.LEFT+IIF(THIS.PARENT.PARENT.RECORDMARK,10,0) && учитываем ширину RECORDMARK
		* Сканируем видимые колнки и учитываем их ширину
	    FOR EACH loI IN THIS.PARENT.PARENT.COLUMNS
	        IF BETWEEN(loI.COLUMNORDER, ;
	            THIS.PARENT.PARENT.LEFTCOLUMN, ;
	            THIS.PARENT.COLUMNORDER-1)
	            .LEFT=.LEFT+loI.WIDTH+1
	        ENDIF
	    ENDFOR
		*
	    .SPECIALEFFECT=0
	    .VISIBLE=.T.
		* Добавляем второй контейнер для создания объемных крав
	    .ADDOBJECT("loBorder","CONTAINER")
	    WITH .loBorder
	        .TOP=2
	        .LEFT=2
	        .WIDTH=.PARENT.WIDTH-4
	        .HEIGHT=.PARENT.HEIGHT-4
	        .BACKCOLOR=RGB(0,128,192)
	        .SPECIALEFFECT=1
	        .VISIBLE=.T.
	    ENDWITH
		* Добавляем объект Label, в который будем выводить набираемый при поиске текст
	    .ADDOBJECT("loSearchText","LABEL")
	    WITH .loSearchText
	        .CAPTION=THIS.SearchSrt
	        .FORECOLOR=RGB(255,255,255)
	        .AUTOSIZE=.T.
	        .BACKSTYLE=0
	        .LEFT=5
	        .FONTNAME="MS Sans Serif"
	        .FONTSIZE=8
	        .TOP=(.PARENT.HEIGHT-FONTMETRIC(1,.FONTNAME,.FONTSIZE))/2
	        .VISIBLE=.T.
	    ENDWITH
	ENDWITH
	*
	m.lcFieldName=THIS.PARENT.CONTROLSOURCE && название поля
	* ширина поля символов
	IF THIS.TypeField='D'
	    m.lnFieldLen=IIF(SET("Century")="ON",10,8)
	ELSE
	    IF RAT('.',m.lcFieldName)>0
	        IF TYPE("Fsize(UPPER(SUBSTR(m.lcFieldName,RAT('.',m.lcFieldName)+1)),LEFT(m.lcFieldName,AT('.',m.lcFieldName)-1))") = 'N'
	            m.lnFieldLen=FSIZE(UPPE(SUBS(m.lcFieldName,RAT('.',m.lcFieldName)+1)),LEFT(m.lcFieldName,AT('.',m.lcFieldName)-1))
	        ELSE
	            m.lnFieldLen=30 && если мы не смогли вычислить длянну поля
	        ENDIF
	    ELSE
	        m.lnFieldLen=FSIZE(m.lcFieldName)
	    ENDIF
	ENDIF
	*
	IF THIS.TypeField='C'
	    THIS.ComparisonExpression="UPPER("+m.lcFieldName+')'
	ELSE
	    THIS.ComparisonExpression="PADR("+m.lcFieldName+','+ALLTRIM(STR(m.lnFieldLen))+')'
	ENDIF

ENDPROC
PROCEDURE search
********************************************************************************
* Непосредственно сама функция поиска
********************************************************************************
LPARAMETERS  tnContinueSearch
LOCAL lnOldRecno as Integer
PRIVATE pcRequiredLine
	THIS.isSeekMode=.T.
	pcRequiredLine=THIS.SearchSrt
	lnOldRecno=IIF(EOF() OR BOF(),0,RECNO())
	lcLocateExpression=[LOCATE FOR ]+THIS.ComparisonExpression+[=m.pcRequiredLine]
	IF m.tnContinueSearch=1
	    &lcLocateExpression
	ELSE
	    IF FOUND()
	        CONTINUE
	    ELSE
	        &lcLocateExpression
	    ENDIF
	ENDIF
	*
	IF !FOUND()
	    IF m.tnContinueSearch=1
	        THIS.SearchSrt = LEFT(THIS.SearchSrt,LEN(THIS.SearchSrt)-1)
	        THIS.SearchWindow.loSearchText.CAPTION=THIS.SearchSrt
	    ENDIF
	    IF m.lnOldRecno>0
	        GO m.lnOldRecno
	    ENDIF
	    THIS.SearchWindow.loBorder.BACKCOLOR=RGB(255,0,128)
	    THIS.PARENT.PARENT.REFRESH
	    RETURN .F.
	ELSE
	    THIS.SearchWindow.loBorder.BACKCOLOR=RGB(0,128,192)
	    THIS.PARENT.PARENT.REFRESH
	    RETURN .T.
	ENDIF

ENDPROC
[END METHODS]
[START RESERVED1]
Class[END RESERVED1]
[START RESERVED2]
1[END RESERVED2]
[START RESERVED3]
*initsearch Инициализация нового поиска
*search Функция выполняющая поик
comparisonexpression Выражение для сравнения с искомой строкой
isactivesearch Активен ли поиск
isseekmode Флаг подавления  GotFocus/LostFocus  во время поиска
searchsrt Искомая строка
searchwindow Ссылка на окно вывода искомого текста 
typefield Тип данных в поле
[END RESERVED3]
[START RESERVED6]
Pixels[END RESERVED6]

[ RECORD]
[PLATFORM] COMMENT 
[UNIQUEID] RESERVED  
[OBJNAME] inctext
[START PROPERTIES]
MS Sans Serif, 0, 9, 5, 13, 11, 11, 2, 0
[END PROPERTIES]
[EOF]
